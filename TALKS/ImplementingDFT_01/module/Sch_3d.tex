\section{Schroedinger equation in 3d}

The 3d case of Schroedinger equation is a straightforward extension of the 2d case.
The Schroedinger equation thus reads:
\begin{equation}
\left[ -\frac{1}{2}\nabla^2 + V(x,y,z) \right] \psi(x,y,z) = E\,\psi(x,y,z)
\label{eq:sch_3d}
\end{equation}
%
where $\nabla^2$ is the Laplacian operator:
\begin{equation}
\nabla^2 = \frac{\partial^2}{\partial x^2} + \frac{\partial^2}{\partial y^2} + \frac{\partial^2}{\partial z^2}
\end{equation}

We begin by defining a struct called \txtinline{FD3dGrid} which is a
straightforward generalization of \txtinline{FD2dGrid}. The implementation of this
struct can be found in the file \txtinline{FD3d/FD3dGrid.jl}.

The Lagrangian operator in 3d also can be implemented by straightforward extension
of 2d case.
\begin{juliacode}
const ⊗ = kron

function build_nabla2_matrix( fdgrid::FD3dGrid; func_1d=build_D2_matrix_3pt )

    D2x = func_1d(fdgrid.Nx, fdgrid.hx)
    D2y = func_1d(fdgrid.Ny, fdgrid.hy)
    D2z = func_1d(fdgrid.Nz, fdgrid.hz)

    IIx = speye(fdgrid.Nx)
    IIy = speye(fdgrid.Ny)
    IIz = speye(fdgrid.Nz)

    ∇² = D2x⊗IIy⊗IIz + IIx⊗D2y⊗IIz + IIx⊗IIy⊗D2z 

    return ∇²
end
\end{juliacode}

The main difference is that we have used the symbol \txtinline{⊗} in place
of \txtinline{kron} function to make our code simpler.

We hope that at this point you will have no difficulties to create your own
3d Schroedinger equation solver.

Analytic solution for energy:
\begin{equation}
E_{n_{x} + n_{y} + n_{z}} = \hbar \omega \left( n_{x} + n_{y} + n_{z} + \frac{3}{2} \right)
\end{equation}

Degeneracies:
\begin{equation}
g_{n} = \frac{(n + 1)(n + 2)}{2}
\end{equation}

\begin{textcode}
n = n_x + n_y + n_z

n = 0: (1)(2)/2 = 1
n = 1: (2)(3)/2 = 3
n = 2: (3)(4)/2 = 6
\end{textcode}

\subsection{Hydrogen atom}

Until now, we only have considered simple potentials such as harmonic potential. Now we will
move on and consider more realistic potentials which is used in practical electronic calculations.

For most applications in materials physics and chemistry the external potential that is
felt by electrons is the Coulombic potential due to atomic nucleus. This potential has
the following form:
\begin{equation}
V(r) = -\sum_{I}^{N_{\mathrm{atom}}} \frac{Z_{I}}{\left|\mathbf{r} - \mathbf{R}_{I}\right|}
\end{equation}
where $R_{I}$ are the positions and $Z_{I}$ are the charges
of the atomic nucleus present in the system.
%
We will consider the most simplest system, namely the hydrogen atom $Z_{I}=1$, for which we have
\begin{equation}
V(r) = -\frac{1}{\left|\mathbf{r} - \mathbf{R}_{0}\right|}
\end{equation}
%
The following Julia code implement the H atom potential:
\begin{juliacode}
function pot_H_atom( fdgrid::FD3dGrid; r0=(0.0, 0.0, 0.0) )
    Npoints = fdgrid.Npoints
    Vpot = zeros(Npoints)
    for i in 1:Npoints
        dx = fdgrid.r[1,i] - r0[1]
        dy = fdgrid.r[2,i] - r0[2]
        dz = fdgrid.r[3,i] - r0[3]
        Vpot[i] = -1.0/sqrt(dx^2 + dy^2 + dz^2)
    end
    return Vpot
end
\end{juliacode}

With only minor modification to our program for harmonic potential, we can solve the Schroedinger
equation for the hydrogen atom:
\begin{juliacode}
fdgrid = FD3dGrid( (-5.0,5.0), Nx, (-5.0,5.0), Ny, (-5.0,5.0), Nz )
∇2 = build_nabla2_matrix( fdgrid, func_1d=build_D2_matrix_9pt )
Vpot = pot_H_atom( fdgrid )
Ham = -0.5*∇2 + spdiagm( 0 => Vpot )
prec = aspreconditioner(ruge_stuben(Ham))
Nstates = 1  # only choose the lowest lying state
Npoints = Nx*Ny*Nz
X = ortho_sqrt( rand(Float64, Npoints, Nstates) ) # random initial guess of wave function
evals = diag_LOBPCG!( Ham, X, prec, verbose=true )
\end{juliacode}

For the grid size of $N_{x}=N_{y}=N_{z}=50$ and using 9-point finite-difference approximation
to the second derivative operator in 1d we obtain the eigenvalue of -0.4900670759 Ha which
is not too bad if compared with the exact value of -0.5 Ha.

\begin{juliacode}
function pot_Hps_HGH( fdgrid::FD3dGrid; r0=(0.0, 0.0, 0.0) )
    Npoints = fdgrid.Npoints
    Vpot = zeros( Float64, Npoints )

    # Parameters
    Zval = 1
    rloc = 0.2
    C1 = -4.0663326
    C2 = 0.6678322
    for ip = 1:Npoints
        dx2 = ( fdgrid.r[1,ip] - r0[1] )^2
        dy2 = ( fdgrid.r[2,ip] - r0[2] )^2
        dz2 = ( fdgrid.r[3,ip] - r0[3] )^2
        r = sqrt(dx2 + dy2 + dz2)
        if r < eps()
            Vpot[ip] = -2*Zval/(sqrt(2*pi)*rloc) + C1
        else
            rrloc = r/rloc
            Vpot[ip] = -Zval/r * erf( r/(sqrt(2.0)*rloc) ) +
                     (C1 + C2*rrloc^2)*exp(-0.5*(rrloc)^2)
        end
    end
    return Vpot
end
\end{juliacode}
